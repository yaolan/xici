apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "net.xici.newapp"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"

        // dex突破65535的限制
        multiDexEnabled true
        // 默认是umeng的渠道
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "xici"]
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        debug {
            // No debug config
        }

        release {
            storeFile file("../key")
            storePassword "123456"
            keyAlias "xici.net"
            keyPassword "xici.net"
        }
    }

    buildTypes {

        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"

            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }

        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"

            minifyEnabled false
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为xianxing_v1.0_2015-01-15_wandoujia.apk
                        def fileName = "xianxing_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }

        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    // 友盟多渠道打包
    productFlavors {
        xici {}
        xiaomi {}
        baidu {}
        m360 {}
        hiapk {}
        anzhi {}
        appchina {}
        huawei {}
        appqq {}
        m91 {}
        wandoujia {}
        meizu {}
        mumayi {}
        lenovo {}
        eoeandroid {}

    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    //google
    compile 'com.android.support:support-v4:22.2.0'
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:design:22.2.0'
    //第三方
    compile 'com.anupcowkur:reservoir:2.0'
    //缓存
    compile 'de.greenrobot:eventbus:2.4.0'
    //eventbus 事件总线
    compile 'com.orhanobut:logger:1.4'
    //日志
    compile 'com.afollestad:material-dialogs:0.7.6.0'
    //https://github.com/afollestad/material-dialogs
    compile project(':social_sdk_library_project')
    compile files('libs/nineoldandroids-2.4.0.jar')
}
